buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url 'https://maven.fabric.io/public'
        }
    }
    dependencies {
        classpath 'org.pegdown:pegdown:1.6.0'
        //noinspection GradleDynamicVersion
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

repositories {
    maven {
        url 'https://maven.fabric.io/public'
    }
}

/*
generate HTML-files from Markdown-files for including them in the app
 */

import org.pegdown.PegDownProcessor

task releaseNotes() {
    def processor = new PegDownProcessor()

    new File('app/src/main/assets/about.html').withWriter('UTF-8') { w ->
        w.write(processor.markdownToHtml(new File("ABOUT.md").text))
    }

    new File('app/src/main/assets/changelog.html').withWriter('UTF-8') { w ->
        w.write(processor.markdownToHtml(new File("CHANGELOG.md").text))
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 21
    //noinspection GradleDependency
    buildToolsVersion "21.1.1"

    defaultConfig {
        applicationId "de.eightbitboy.hijacr"
        minSdkVersion 14
        //noinspection OldTargetApi
        targetSdkVersion 21
        versionCode 2
        versionName "0.1.1"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    /*
    android libraries
     */
    compile 'com.android.support:appcompat-v7:22.1.1'
    compile 'com.android.support:design:22.2.1'
    compile 'com.android.support:support-annotations:23.0.1' //TODO use this

    /*
    third-party libraries
     */
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'de.greenrobot:eventbus:2.4.0'
    compile 'org.sufficientlysecure:html-textview:1.2'
    compile 'org.jsoup:jsoup:1.8.3'
    compile 'com.orhanobut:logger:1.11'
    compile 'com.github.chrisbanes.photoview:library:1.2.3'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.4'

    compile('com.crashlytics.sdk.android:crashlytics:2.5.2@aar') {
        transitive = true;
    }

    /*
    testing
     */
    androidTestCompile 'com.android.support.test:runner:0.4.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'
}
